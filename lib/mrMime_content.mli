(** Module Content
 
    This goal of this module is about the MIME header specification.  So you can
    find somes mechanisms:

    - A {!MrMime_mimeVersion.version} header field,  which uses a version number
    to declare a  message to be conformant with MIME  and allows mail processing
    agents to distinguish between such messages  and those generated by older or
    non-conformant software, which are presumed to lack such a field.

    -   A   {!MrMime_contentType.content}   header   field,   generalized   from
    {{:https://tools.ietf.org/html/rfc1049}RFC1049},   which  can   be  used  to
    specify the media type  and subtype of data in the body  of a message and to
    fully specify the native representation (canonical form) of such data.

    - A {!MrMime_contentEncoding.mechanism} header  field,  which can be used to
    specify both  the encoding transformation that  was applied to  the body and
    the domain of the result.  Encoding  transformations other than the identity
    transformation are  usually applied  to data in  order to  allow it  to pass
    through  mail transport  mechanims which  may  have  data  or  character set
    limitations.

    - Two additional header fields that can be used to further describe the data
    in a body, the ["Content-ID"] and ["Content-Description"] header fields.

    If you want to joke about the shit of the email, we can expose a good note
    to explain why the email is hard to parse:

    {e Several  of the mechanisms  described in this  set of documents  may seem
    somewhat strange or even baroque at  first reading.  It is important to note
    that compatibility  with existing standards  AND robustness  across existing
    practice were two  of  the  highest  priorities  of  the  working group that
    developed this  set of documents.  In  particular,  compatibility was always
    favored over elegance.}

    @see <https://tools.ietf.org/html/rfc2045#section-1> RFC2045 § 1
*)

(** Map with [type key = string]. *)
module Map            : module type of Map.Make(String)

(** It's             an             {i             encoded-word}            from
    {{:https://tools.ietf.org/html/rfc2047}RFC2047}.  An  {i encoded-word}  is a
    sequence of printable  ASCII characters that begins  with ["=?"],  ends with
    ["?="],  and has two ["?"]s in between.  It specifies a character set and an
    encoding method,  and  also includes  the original  text encoded  as graphic
    ASCII characters, according to the rules for that encoding method.

    MrMime recognizes {i encoded-words} when  they appear in certain protions of
    the message header.  Instead of displaying the {i encoded-word} "as is",  it
    will reverse the encoding and display the original text.

    {b NOTE}: the client need to translate the original text into the designated
    character set (like [utf-8]) - this feature is in {b TODO}.

    @see <https://tools.ietf.org/html/rfc2047> RFC2047
*)
type raw              = Rfc2047.raw =
  | QuotedPrintable of string
  | Base64 of MrMime_base64.result

(** Some                    field                    bodies                   in
    {{:https://tools.ietf.org/html/rfc5322#section-2.2.1}RFC5322}  specification
    are defined simply as {i  unstructured} with no further restrictions.  These
    are referred  to as unstructured  field bodies.  Semantically,  unstructured
    field bodies are simply to be treated as a single line of characters with no
    further processing (except for {i folding} and {i unfolding} white space).

    MrMime keeps somes informations:
    - [`Text s] is just a [string]
    - [`CR n] when MrMime decodes only somes ["\r"]s characters (withtout ["\n"]
    afterwards)
    - [`LF n] when MrMime decodes only somes ["\n"]s characters
    - [`CRLF]  when MrMime decodes  a {i folding  whitespace} without whitespace
    (like ["\r\n"])
    - [`WSP] when MrMime decodes a whitespace (like [" "] or ["\t"])
    - [`Encoded  (character_set,  raw)] when MrMime decodes  an {i encoded-word}
    (see {!raw})
*)
type unstructured     =
  [ `Text of string | `CR of int | `LF of int | `CRLF | `WSP
  | `Encoded of (string * raw) ] list

(** MIME defines  a number of  new {{:https://tools.ietf.org/html/rfc822}RFC822}
    header fields that are used to describe the content of a MIME entity.  These
    headers fields occur in at least two context:

    - As part of a regular {{:https://tools.ietf.org/html/rfc822}RFC822} message
    header.
    - In a MIME body part header within a multipart construct.

    As  {!MrMime_header.field},  MrMime can  skip somes  lines with  the [`Skip]
    constructor.    That    means   MrMime    can't    use    any    rule   from
    {{:https://tools.ietf.org/html/rfc2045#section-3}RFC2045} for  this line and
    keep the data without any processing.

    Another point  and ever as  {!MrMime_header.field},  if MrMime  recognizes a
    header                  field                   decribed                  by
    {{:https://tools.ietf.org/html/rfc2045#section-3}RFC2045} but it can't apply
    the formal definition, it returns [`Unsafe] with the {!unstructured} value -
    afterwards, the client can do a weird process.
*)
type field            = [ Rfc2045.field | Rfc2045.field_version | Rfc2045.skip ]

(** A convenience record to deal with any ["Content-*"] fields. *)
type t =
  { ty                : MrMime_contentType.content
    (** The ["Content-Type"] field. *)
  ; encoding          : MrMime_contentEncoding.mechanism
    (** The ["Content-Transfer-Encoding"] field. *)
  ; version           : MrMime_mimeVersion.version
    (** The ["MIME-Version"] field. *)
  ; id                : MrMime_msgID.msg_id option
    (** The ["Content-ID"] field.  In  constructing a high-level user-agent,  it
        may  be desirable  to allow  one  body  to  make  reference  to another.
        Accordingly,  bodies may  be  labelled  using  the ["Content-ID"] header
        field,  which is  syntactically identical  to the  ["Message-ID"] header
        field.

        MrMime does not process the ["Content-ID"] as a reference or as a common
        representation  between somes  bodies  with  a ["multipart/alternative"]
        media-type (which describes a different semantic of this field).  MrMime
        just parses this  information and nothing else (like  some extra check).
        But we can explain the semantic of this field in this document.

        The  ["Content-ID"] value  may be  used  for  uniquely  identifying MIME
        entities in several contexts,  particularly  for caching data referenced
        by the ["message/external-body"] mechanism.  Although the ["Content-ID"]
        header is  generally optional,  its use is  mandatory in implementations
        which   generate    data    of    the    optional    MIME   media   type
        ["message/external-body"] (but MrMime does not check that).

        It  is also  worth noting  that  the  ["Content-ID"]  value  has special
        semantics in the cas of the ["multipart/alternative"] media type.

        Each part  of  a  ["multipart/alternative"]  entity  represents the same
        data,  but the mappings  between  the  two  are  not necessarily without
        information loss.  For example, information is lost when translating ODA
        to PostScript  or plain text.  It  is recommended that  each part should
        have a different ["Content-ID"] value  in the case where the information
        content of  the two  parts is not  identical.  And when  the information
        content  is  identical  -  for  exemple,  where  several  parts  of type
        ["message/external-body"] specify alternate ways to access the identical
        data - the same ["Content-ID"]  field value should be used,  to optimize
        any caching  mechanisms that  might be present  on the  recipient's end.
        However,  the ["Content-ID"] values used by  the parts should not be the
        same ["Content-ID"]  value that describes  the ["multipart/alternative"]
        as a whole,  if  there is any such  ["Content-ID"] field.  That is,  one
        ["Content-ID"] value will refer to the ["multipart/alternative"] entity,
        while one  or more other ["Content-ID"]  values will refer  to the parts
        inside its.

        @see <https://tools.ietf.org/html/rfc2045#section-7> RFC2045 § 7
        @see <https://tools.ietf.org/html/rfc2046#section-5.1.4> RFC2046 § 5.1.4
     *)
  ; description       : unstructured option
    (** The  ["Content-Description"]  field.   The  ability  to  associate  some
        descriptive  information  with a  given  body  is  often desirable.  For
        example, it may be useful to mark an ["image"] body as "a picture of the
        SpaceShuttle   Endeavoir.".   Such   text   may   be   placed   in   the
        ["Content-Description"] header field. This hader is always optional.

        @see <https://tools.ietf.org/html/rfc2045#section-8> RFC2045 § 8
    *)
  ; content           : unstructured list Map.t
    (** Future documents may  elect to define additional MIME  header fields for
        various  purposes.  Any  new header  field  that  further  describes the
        content of a message should begin  with the string ["Content-"] to allow
        such fields  which appear in a  message header to  be distinguished from
        ordinary  {{:https://tools.ietf.org/html/rfc822}RFC822}  message  header
        fields.

        MrMime considers all ["Content-*"] fields as an {!unstructured} value.

        @see <https://tools.ietf.org/html/rfc2045#section-9> RFC2045 § 9
        @see <https://tools.ietf.org/html/rfc822#section-4.7> RFC822 § 4.7.4 & 4.7.5
        @see <https://tools.ietf.org/html/rfc5322#section-3.6.8> RFC5322 § 3.6.8
    *)
  ; unsafe            : unstructured list Map.t
    (** As explained  for the [`Unsafe]  {!field},  when MrMime can't  apply the
        formal     definition     of     a     field     described     by    the
        {{:https://tools.ietf.org/html/rfc2045#section-3}RFC2045},  it considers
        the field as  an {!unstructured} value to  let the client to  do a weird
        processing.
    *)
  ; skip              : string list
    (** As explained for the [`Skip] {!field},  when MrMime can't apply any rule
        for a line,  it stores this line  in the [skip] field.  MrMime keeps the
        order of the appearance inside the email.

        Generally, this field is empty.
    *)
  }

(** [pp_raw raw] prints an human readable representation of {!raw}. *)
val pp_raw            : Format.formatter -> Rfc2047.raw -> unit

(** [pp_unstructured v] prints an human readable representation of {!unstructured}. *)
val pp_unstructured   : Format.formatter -> unstructured -> unit

(** [pp_field field] prints an human readable representation of {!field}. *)
val pp_field          : Format.formatter -> field -> unit

(** [pp content] prints an human readable representation of {!t}. *)
val pp                : Format.formatter -> t -> unit

(** According                  to                 {!MrMime_contentType.default},
    {!MrMime_contentEncoding.default} and {!MrMime_mimeVersion.default}, we have
    a default value of a content header.
*)
val default           : t

module Encoder :
sig
  val w_field         : (Rfc2045.field, 'r Encoder.partial) Encoder.k1
  val w_field_version : (Rfc2045.field_version, 'r Encoder.partial) Encoder.k1
  val w_unsafe        : (Rfc2045.unsafe, 'r Encoder.partial) Encoder.k1
  val w_skip          : (Rfc2045.skip, 'r Encoder.partial) Encoder.k1

  val w_message       : (t, 'r Encoder.partial) Encoder.k1
  val w_part          : (t, 'r Encoder.partial) Encoder.k1
end

module Decoder :
sig
  (** See RFC2045 § {{:https://tools.ietf.org/html/rfc2045#section-3}3}:

      {[
      entity-headers := [ content CRLF ]
                        [ encoding CRLF ]
                        [ id CRLF ]
                        [ description CRLF ]
                        *( MIME-extension-field CRLF )

      MIME-message-headers := entity-headers
                              fields
                              version CRLF
                              ; The ordering of the header
                              ; fields implied by this BNF
                              ; definition should be ignored.
      ]}

      {b NOTE}:  this  decoder has no  [`Skip] and  [`Unsafe] constructor.  This
      things is let to {!MrMime_header.Decoder.header}.
  *)
  val message         : ([> Rfc2045.field | Rfc2045.field_version ] as 'a) list -> (t * 'a list) MrMime_parser.t

  (** See RFC2045 § {{:https://tools.ietf.org/html/rfc2045#section-3}3}:

     {[
     entity-headers := [ content CRLF ]
                       [ encoding CRLF ]
                       [ id CRLF ]
                       [ description CRLF ]
                       *( MIME-extension-field CRLF )

     MIME-part-headers := entity-headers
                          [ fields ]
                          ; Any field not beginning with
                          ; "content-" can have no defined
                          ; meaning and may be ignored.
                          ; The ordering of the header
                          ; fields implied by this BNF
                          ; definition should be ignored.
     ]}
  *)
  val part            : ([> Rfc2045.field | Rfc2045.unsafe | Rfc2045.skip ] as 'a) list -> (t * 'a list) MrMime_parser.t
end
